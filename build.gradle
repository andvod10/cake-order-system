plugins {
    id 'java-library'
    id 'java'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 11
targetCompatibility = 11

ext {
    jacksonVersion = "2.14.2"
    junitVersion = "4.13.2"
    mapStructVersion = "1.5.5.Final"
    lombokVersion = "1.18.30"
}

dependencies {
    api(
            "com.amazonaws:aws-lambda-java-core:1.2.3",
            "com.amazonaws:aws-lambda-java-events:3.1.0",
            "com.amazonaws:aws-lambda-java-log4j:1.0.1",
            "com.amazonaws:aws-java-sdk-dynamodb:1.12.573",
            "com.amazonaws:aws-java-sdk-kinesis:1.12.579",
            "com.amazonaws:aws-java-sdk-ses:1.12.583",
            "com.amazonaws:aws-java-sdk-sqs:1.12.585",

            "com.fasterxml.jackson.core:jackson-core:$jacksonVersion",
            "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion",
            "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion",
            "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion"
    )

    compileOnly "org.mapstruct:mapstruct:$mapStructVersion"
    annotationProcessor "org.mapstruct:mapstruct-processor:$mapStructVersion"

    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:0.2.0"
}

// Task for building the zip file for upload
task buildZip(type: Zip) {
    // Using the Zip API from gradle to build a zip file of all the dependencies
    //
    // The path to this zip file can be set in the serverless.yml file for the
    // package/artifact setting for deployment to the S3 bucket
    //
    // Link: https://docs.gradle.org/current/dsl/org.gradle.api.tasks.bundling.Zip.html

    // set the base name of the zip file
    archiveBaseName = "cake-ordering-system"
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtimeClasspath
    }
}

build.dependsOn buildZip
